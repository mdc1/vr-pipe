#!/usr/bin/env perl
use strict;
use warnings;

=head1 AUTHOR

Sendu Bala <sb10@sanger.ac.uk>.

=head1 COPYRIGHT AND LICENSE

Copyright (c) 2012 Genome Research Limited.

This file is part of VRPipe.

VRPipe is free software: you can redistribute it and/or modify it under the
terms of the GNU General Public License as published by the Free Software
Foundation, either version 3 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program. If not, see L<http://www.gnu.org/licenses/>.

=cut

use EV;
use AnyEvent;
use AnyEvent::Util qw(fork_call);
use AnyEvent::ForkManager;
use Sys::CPU;
use VRPipe::FrontEnd;
use VRPipe::Interface::BackEnd;

# handle options
my $cmdline = VRPipe::FrontEnd->new(
    description => "Handle the running of setups or submissions while executing on a farm node.",
    opt_spec    => [
        ['One of two modes must be specified, as must --farm.'],
        ['mode|m=s', 'setups|submissions - handle setups or submissions'],
        ['farm=s',   'Handle setups being controlled by this farm'],
        [],
        ['In --mode submissions, you must also supply:'],
        ['requirements|r=i', 'a Requirements id to define which submissions we should try to run', { persistent_object => 'Requirements' }],
        ['scheduler|s=i',    'a Scheduler id',                                                     { persistent_object => 'Scheduler' }],
        ['runner=i',         'a Runner id',                                                        { persistent_object => 'Runner' }]
    ]
);

my $max_forks = Sys::CPU::cpu_count();

# initialize the VRPipe backend
my $deployment = $cmdline->opts('deployment');
my $backend    = VRPipe::Interface::BackEnd->new(deployment => $deployment);
my $schema     = $backend->schema;
$AnyEvent::Util::MAX_FORKS = $max_forks;

# act on the given mode
my $mode = $cmdline->opts('mode') || $cmdline->die_with_error("--mode must be supplied");
my $farm = $cmdline->opts('farm') || $cmdline->die_with_error("--farm is required");
my ($fs) = VRPipe::FarmServer->search({ farm => $farm });
unless ($fs) {
    $cmdline->die_with_error("There is no farm server running for farm '$farm'");
}

if ($mode eq 'setups') {
    handle_setups();
}
elsif ($mode eq 'submissions') {
    my $req = $cmdline->opts('requirements');
    $req || $cmdline->die_with_error("--requirements is required in submissions mode");
    my $scheduler = $cmdline->opts('scheduler');
    $scheduler || $cmdline->die_with_error("--scheduler is required in submissions mode");
    my $runner = $cmdline->opts('runner');
    $runner || $cmdline->die_with_error("--runner is required in submissions mode");
    handle_submissions($req, $scheduler, $runner);
}
else {
    $cmdline->die_with_error("$mode is an invalid mode");
}

exit 0;

sub handle_setups {
    $backend->log_stderr;
    
    # every 30s we'll check for new setups we're supposed to handle, and also
    # confirm there is still a farmserver up for our farm (and exit if not)
    my %setups;
    my %datasource_watchers;
    my %setup_watchers;
    my $setups_timer_interval      = $deployment eq 'production' ? 30  : 5;
    my $datasource_timer_interval  = $deployment eq 'production' ? 300 : 10;
    my $setup_state_timer_interval = $deployment eq 'production' ? 900 : 15;
    my $setups_watcher = EV::timer 0, $setups_timer_interval, sub {
        my @setups = ();
        eval {
            $fs->alive || die "no farmserver";
            @setups = VRPipe::PipelineSetup->search({ active => 1, controlling_farm => $farm }, { prefetch => ['datasource', 'pipeline'] });
        };
        if ($@) {
            if ($@ =~ /no farmserver/) {
                exit_handler("There is no longer a server for farm $farm");
            }
            else {
                exit_handler("There was a problem accessing the database: $@");
            }
        }
        else {
            if (@setups) {
                # remove handling and watchers for setups no longer active/
                # controlled by our farm
                my %current_setup_ids = map { $_->id => 1 } @setups;
                foreach my $stored_setup_id (keys %setups) {
                    unless (exists $current_setup_ids{$stored_setup_id}) {
                        my $ds_id = $setups{$stored_setup_id}->datasource->id;
                        $datasource_watchers{$ds_id}->{count}--;
                        delete $datasource_watchers{$ds_id} if $datasource_watchers{$ds_id}->{count} == 0;
                        delete $setup_watchers{$stored_setup_id};
                        delete $setups{$stored_setup_id};
                    }
                }
                
                # trigger new setups, in parallel
                my $fm = AnyEvent::ForkManager->new(max_workers => $max_forks);
                my @new_setups;
                foreach my $setup (@setups) {
                    my $sid = $setup->id;
                    next if exists $setups{$sid};
                    $setups{$sid} = $setup;
                    push(@new_setups, $setup);
                    
                    $fm->start(
                        cb => sub {
                            my ($fm, $setup) = @_;
                            $setup->trigger;
                        },
                        args => [$setup]
                    );
                }
                $fm->wait_all_children;
                
                # when testing, we don't want to have this keep running
                # after all the setups have completed
                if ($deployment eq 'testing') {
                    my $completed = 0;
                    foreach my $setup (@setups) {
                        $completed++ if $setup->currently_complete;
                    }
                    exit_handler("All the setups are currently complete") if $completed == @setups;
                }
                
                # create a watcher for each setup's datasource to keep it
                # up-to-date
                foreach my $setup (@new_setups) {
                    my $ds    = $setup->datasource;
                    my $ds_id = $ds->id;
                    $datasource_watchers{$ds_id}->{count}++;
                    next if defined $datasource_watchers{$ds_id}->{watcher};
                    
                    my $datasource_update_sub = sub {
                        fork_call {
                            $ds->elements;
                            return;
                        }
                        sub {
                            my $err = $@ || return;
                            # email user about this once
                            #*** auto-deactivate it?
                            $backend->log("There is a problem with datasource $ds_id:\n$err");
                        };
                    };
                    
                    my $non_persistent = $ds->_from_non_persistent;
                    my $watcher;
                    if ($non_persistent && $non_persistent->does('VRPipe::DataSourceTextRole')) {
                        # update when the source text file changes
                        $watcher = EV::stat $ds->source, 0, $datasource_update_sub;
                    }
                    else {
                        # update every 5mins
                        $watcher = EV::timer 0, $datasource_timer_interval, $datasource_update_sub;
                    }
                    
                    $datasource_watchers{$ds_id}->{watcher} = $watcher;
                }
                
                # create a watcher to check the state of each setup, so we
                # can notify user of stalled/ failed/ completed ones
                foreach my $setup (@new_setups) {
                    my $sid = $setup->id;
                    next if defined $setup_watchers{$sid};
                    
                    $setup_watchers{$sid} = EV::timer 0, $setup_state_timer_interval, sub {
                        fork_call {
                            return if $setup->currently_complete;
                            
                            my $perma_fails = VRPipe::Submission->search({ '_done' => 0, '_failed' => 1, retries => 3, 'stepstate.pipelinesetup' => $sid }, { join => ['stepstate'] });
                            if ($perma_fails) {
                                # email user once about this
                                #***
                                $backend->log("setup $sid has repeated failures that need user intervention");
                            }
                            else {
                                my $incomplete_subs = VRPipe::Submission->search({ '_done' => 0, '_failed' => 0, 'stepstate.pipelinesetup' => $sid }, { join => ['stepstate'] });
                                unless ($incomplete_subs) {
                                    $backend->log("forcing a full trigger for setup $sid because we aren't complete, but have no incomplete submissions");
                                    $setup->trigger;
                                }
                            }
                            
                            return;
                        }
                        sub {
                            my $err = $@ || return;
                            # email user about this once
                            #*** auto-deactivate it?
                            $backend->log("There is a problem with setup $sid:\n$err");
                        };
                    };
                }
            }
            else {
                exit_handler("There are no longer any more setups to handle for farm $farm");
            }
        }
    };
    
    # set up the signals we'll react to
    $SIG{HUP}  = 'IGNORE';
    $SIG{PIPE} = 'IGNORE';
    my $sigterm_watcher = EV::signal 'TERM', sub { exit_handler("SIGTERM received"); };
    my $sigint_watcher  = EV::signal 'INT',  sub { exit_handler("SIGINT received"); };
    my $sigquit_watcher = EV::signal 'QUIT', sub { exit_handler("SIGQUIT received"); };
    
    # run the event loop
    $backend->log("The setups handler for farm $farm is now running.");
    EV::run;
    
    $backend->log("The setups handler for farm $farm is now exiting!");
    exit 0;
}

sub handle_submissions {
    my ($req, $scheduler, $runner) = @_;
    $backend->log_stderr;
    my $req_id      = $req->id;
    my $time_needed = $req->time;
    my $end_time    = (time() + $scheduler->queue_time($req)) - 65;
    $req->disconnect;
    
    # every 60s we'll pick a submission if we don't have one already, and update
    # its status
    my $timer_interval = $deployment eq 'production' ? 60 : 5;
    my ($submission, $job_pid, %failed_by_us);
    my $watcher = EV::timer 0, $timer_interval, sub {
        if ($submission) {
            $backend->log("updating stats of submission " . $submission->id);
            my $job = $submission->job;
            if ($job->end_time) {
                $submission->update_status;
                
                if ($submission->done) {
                    # we completed successfully; if this was the last submission
                    # of its step and they're all done now, do a trigger for
                    # this dataelement to create submissions for the next step
                    my $stepstate_id = $submission->stepstate->id;
                    
                    my $transaction = sub {
                        # compare counts of total submissions for our step to
                        # count of those done to see if the step is complete.
                        # We can't just get quick counts though otherwise the
                        # for => update lock doesn't do anything
                        my @step_subs = VRPipe::Submission->search({ stepstate => $stepstate_id }, { for => 'update' });
                        my $done = 0;
                        foreach my $sub (@step_subs) {
                            $done++ if $sub->done;
                        }
                        
                        if ($done == @step_subs) {
                            my $step_state = $submission->stepstate;
                            $step_state->pipelinesetup->trigger($step_state->dataelement);
                        }
                    };
                    $submission->do_transaction($transaction, "Failed to see if the step was done after submission " . $submission->id . " completed and trigger the next step");
                }
                elsif ($submission->failed) {
                    # Job->run itself would have adjusted $submission's
                    # requirements already if we failed due to running out of
                    # time or memory; we just need to retry it if appropriate
                    $submission->retry if $submission->retries < 3;
                    $failed_by_us{ $submission->id } = 1;
                }
                
                $submission->release;
                undef $submission;
            }
            elsif ($submission->close_to_time_limit(5)) {
                # user's scheduler might kill the submission if it runs too
                # long in the queue it was initially submitted to; avoid
                # this by changing queue as necessary
                my $current_queue = $scheduler->determine_queue($req);
                $submission->extra_time;
                my $new_queue = $scheduler->determine_queue($submission->requirements);
                
                if ($new_queue ne $current_queue) {
                    $scheduler->switch_queue($runner->sid, $new_queue);
                }
                
                # we do not update $req, so that once this $submission has
                # finished we should think that we've run out of time and kill
                # ourselves, keeping vrpipe-server's estimation of how many
                # handlers per req are running nice and accurate
            }
            
            $req->disconnect;
        }
        unless ($submission) {
            # pick the next available submission to work on
            eval {
                $fs->alive || die "no farmserver";
                die "ran out of time" if (time() >= $end_time);
                
                my $survival_time = $fs->survival_time;
                my $dtf           = $schema->storage->datetime_parser;
                
                my $sub_pager = VRPipe::Submission->search_paged({ requirements => $req_id, '_done' => 0, -or => [-and => ['_failed' => 1, retries => { '<' => 3 }], '_failed' => 0], scheduler => $scheduler->id, 'pipelinesetup.controlling_farm' => $farm, 'pipelinesetup.active' => 1, 'job.heartbeat' => [-or => [undef, { '<' => $dtf->format_datetime(DateTime->from_epoch(epoch => time() - $survival_time)) }]] }, { join => { stepstate => 'pipelinesetup' }, prefetch => ['job', { stepstate => { stepmember => 'step' } }] }, 100);
                
                my (%step_limits, %step_counts, %completed_setups);
                SLOOP: while (my $subs = $sub_pager->next(no_resetting => 1)) {
                    foreach my $sub (@$subs) {
                        # if the job is a block_and_skip_if_ok job, we don't
                        # actually block because of race condition issues, and
                        # because of fail and restart issues. Instead we always
                        # only actually submit if this submission is the first
                        # incomplete submission created for this $job.
                        my $job = $sub->job;
                        if ($job->block_and_skip_if_ok) {
                            my ($first_sub) = VRPipe::Submission->search({ 'job' => $job->id, '_done' => 0 }, { rows => 1, order_by => { -asc => 'id' } });
                            next unless $first_sub->id == $sub->id;
                        }
                        
                        # avoid submissions we have failed, in case there is a
                        # node-specific problem
                        if (exists $failed_by_us{ $sub->id }) {
                            # in case all the other handlers have failed it
                            # as well (or in case there are no other
                            # handlers), we'll forget we failed it if we've
                            # considered running this 15 times. This also
                            # gives us a time delay before retrying, in case
                            # there was a transient problem
                            $failed_by_us{ $sub->id }++;
                            if ($failed_by_us{ $sub->id } >= 15) {
                                $backend->log("will forget we failed sub " . $sub->id);
                                delete $failed_by_us{ $sub->id };
                            }
                            $submission = 0; # to stop us exiting due to no submission
                            next;
                        }
                        
                        # global step limit handling
                        my $step    = $sub->stepstate->stepmember->step;
                        my $step_id = $step->id;
                        my $step_limit;
                        if (exists $step_limits{$step_id}) {
                            $step_limit = $step_limits{$step_id};
                        }
                        else {
                            # figure out what the step limit is, if any, by
                            # checking the config of every incomplete & active
                            # setup that uses the step
                            my @setups = VRPipe::PipelineSetup->search({ active => 1, 'step.id' => $step_id }, { join => { 'pipeline' => { 'stepmembers' => 'step' } } });
                            if (@setups) {
                                my $step_name = $step->name;
                                foreach my $setup (@setups) {
                                    unless (exists $completed_setups{ $setup->id }) {
                                        $completed_setups{ $setup->id } = $setup->currently_complete;
                                    }
                                    next if $completed_setups{ $setup->id };
                                    my $setup_limit = $setup->options->{ $step_name . '_max_simultaneous' } || next;
                                    if (!defined $step_limit || $setup_limit < $step_limit) {
                                        $step_limit = $setup_limit;
                                    }
                                }
                            }
                            if (!defined $step_limit) {
                                # also check the step itself for a limit
                                $step_limit = $step->max_simultaneous;
                            }
                            $step_limits{$step_id} = $step_limit || 0;
                            
                            if ($step_limit) {
                                # see how many of this step are currently
                                # running, globally
                                $step_counts{$step_id} = VRPipe::Submission->search({ 'stepmember.step' => $step_id, '_done' => 0, '_failed' => 0, 'job.heartbeat' => { '>' => DateTime->from_epoch(epoch => time() - $survival_time) } }, { join => ['job', { stepstate => 'stepmember' }] });
                            }
                        }
                        if ($step_limit) {
                            $submission = 0;
                            next if $step_counts{$step_id} >= $step_limits{$step_id};
                        }
                        
                        # we'll claim this one to run
                        my $transaction = sub {
                            my ($sub_to_run) = VRPipe::Submission->search({ id => $sub->id }, { for => 'update' });
                            if ($sub_to_run->claim) {
                                $submission = $sub_to_run;
                            }
                        };
                        $sub->do_transaction($transaction, "Failed to claim submission " . $sub->id);
                        last SLOOP if $submission;
                    }
                    $backend->log("had to go to next page to find a submission");
                }
                
                if ($submission) {
                    if ($submission->failed) {
                        # this shouldn't really happen, but perhaps is possible
                        # if vrpipe-handler got killed in between setting the
                        # sub to failed and calling retry; just blindly retry it
                        # now since we've no way of knowing why it failed.
                        $backend->log("blindly retrying submission " . $submission->id);
                        $submission->retry;
                        $submission->release;
                        $submission = 0;
                    }
                    else {
                        # start running the associated job
                        $backend->log("will start running submission " . $submission->id . ", job " . $submission->job->id);
                        $submission->job->run(submission => $submission);
                    }
                }
                
                $req->disconnect;
            };
            if ($@) {
                if ($@ =~ /no farmserver/) {
                    exit_handler("There is no longer a server for farm $farm");
                }
                elsif ($@ =~ /ran out of time/) {
                    exit_handler("There is not enough time to start working on a new submission");
                }
                else {
                    exit_handler("There was a problem picking/ running a new submission: $@");
                }
            }
            elsif (!defined $submission) {
                exit_handler("No submissions to work on");
            }
        }
    };
    
    # run the event loop
    $backend->log("The submissions handler for farm $farm, req " . $req->id . " is now running.");
    EV::run;
    
    $backend->log("The submissions handler for farm $farm, req " . $req->id . " is now exiting!");
    exit 0;
}

sub exit_handler {
    my $msg = shift;
    $backend->log($msg . ", will gracefully exit $mode handler."); # if $mode eq 'setups'
    EV::unloop;
}
